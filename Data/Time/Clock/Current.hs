{-# OPTIONS -ffi -Wall -Werror #-}

-- #hide
module Data.Time.Clock.Current
(
	-- * Current time
	getCurrentTime,
) where

import Data.Time.Clock.UTC

import Foreign
import Foreign.C

data CTimeval = MkCTimeval CLong CLong

ctimevalToPosixSeconds :: CTimeval -> POSIXTime
ctimevalToPosixSeconds (MkCTimeval s mus) = (fromIntegral s) + (fromIntegral mus) / 1000000

instance Storable CTimeval where
	sizeOf _ = (sizeOf (undefined :: CLong)) * 2
	alignment _ = alignment (undefined :: CLong)
	peek p = do
		s   <- peekElemOff (castPtr p) 0
		mus <- peekElemOff (castPtr p) 1
		return (MkCTimeval s mus)
	poke p (MkCTimeval s mus) = do
		pokeElemOff (castPtr p) 0 s
		pokeElemOff (castPtr p) 1 mus

foreign import ccall unsafe "time.h gettimeofday" gettimeofday :: Ptr CTimeval -> Ptr () -> IO CInt

-- | Get the current UTC time from the system clock.
getCurrentTime :: IO UTCTime
getCurrentTime = with (MkCTimeval 0 0) (\ptval -> do
	result <- gettimeofday ptval nullPtr
	if (result == 0)
	 then do
	 	tval <- peek ptval
	 	return (posixSecondsToUTCTime (ctimevalToPosixSeconds tval))
	 else fail ("error in gettimeofday: " ++ (show result))
	)
